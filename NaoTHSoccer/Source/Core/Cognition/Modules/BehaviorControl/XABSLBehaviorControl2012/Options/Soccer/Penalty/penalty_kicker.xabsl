option penalty_kicker
{
  initial state look_for_ball
  {
    decision
    {
      if(ball.was_seen && state_time > 3000)
        goto approach_ball;
      else
        stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
  

  state approach_ball
  {
    decision
    {
      if(action_done)
        goto prepare_kick;
      else
        stay;
    }
    action
    {
      situationStatusOppHalf = true;
      go_to_ball_cool(distance = 170, right_foot=true,
                      errorX=30, errorY=30, errorR=15);
      head.control_mode = search_for_ball;
      motion.walk.style = (ball.distance < 500)?stable:normal;
    }
  }
  

  state prepare_kick
  {
    decision
    {
      if(state_time > 3000)
      {
        if(action_done)
          goto do_kick;
        else
          goto approach_ball;
      }
      else
        stay;
    }
    action
    {
      situationStatusOppHalf = true;
      go_to_ball_cool(distance = 170, right_foot=true,
                      errorX=30, errorY=30, errorR=15);
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }


  state do_kick
  {
    decision
    {
      if(action_done)
        goto play_normal_striker;
      else
        stay;
    }
    action
    {
      situationStatusOppHalf = true;
      kick_ball_with_foot(direction = 0, right = true);
      head.control_mode = search_for_ball;
    }
  }


  state play_normal_striker
  {
    decision
    {
      stay;
    }
    action
    {
      situationStatusOppHalf = false;
      spl_playing_striker();
    }
  }

}