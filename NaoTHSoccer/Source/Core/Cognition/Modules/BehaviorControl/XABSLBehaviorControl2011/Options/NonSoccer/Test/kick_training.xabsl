/** behavior for testing the kick-parameter */
option kick_training {
  
 initial state search_ball {
    decision {
      if (ball.was_seen)
        goto position;
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state position
  {
    decision {
      if(ball.time_since_last_seen > 1000)
      {
        goto search_ball;
      }
      else if(state_time > 2000 && ball.was_seen &&
        (
        ((ball.y <= 0) && (ball.y > -220) && (ball.x < 250)) // ball is right, goal is left
          ||
        ((ball.y > 0) && (ball.y < 220) && (ball.x < 250)) // ball is left, goal is right
        ))
       {
         goto stabilize_before_kick;
       }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state stabilize_before_kick
  {
    decision {
      // stand 1 second before kick
      if(executed_motion.type == stand && executed_motion.time > 1000 && state_time > 1000)
        goto kick;
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state kick {
    decision
    {
      if(action_done)
        goto stabilize_after_kick;
      else
        stay;
    }
    action 
    {
      head.control_mode = search_for_ball;
      kick_ball(direction= 20);
    }
  }

  state stabilize_after_kick {
    decision
    {
      if ( state_time > 1000 )
        goto position;
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }
}