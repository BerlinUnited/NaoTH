/** This behavior approaches the ball 
* and performs an omnidirectional kick
**/

option perform_omnidirectional_kick
{

  initial state stabilize_before_kick
  {
    decision {
      // maybe we touched the ball accidently
      if(abs(value=ball.preview.x) > 300)
        goto finished;
      else if(action_done) //HACK xxx
        goto kick_prep;
      else
        stay;
    }
    action {
      approach_ball_with_correct_foot(dist=170, errorX=30, errorY=30, errorR=15, want_to_turn_after_approach=false);
      head.control_mode = search_for_ball;
      //motion.walk.stop_with_stand = true;
      //motion.type = stand;
      motion.walk.style = (ball.distance < 500)?stable:normal;
      situation.abortable = false;
    }
  }

  state kick_prep
  {
    decision {
         if(state_time > 500)
         {
           if(action_done) {
             goto kick;
           } 
           else {
            goto stabilize_before_kick;
           }
         }
         else
         {
           stay;
         }
    }
    action {
      approach_ball_with_correct_foot(dist=170, errorX=30, errorY=30, errorR=15, want_to_turn_after_approach=false);
      head.control_mode = search_for_ball;
      motion.type = stand;
      situation.abortable = false;
    }
  }

  state kick {
    decision
    {
      if(action_done && state_time > 1000)
        goto stabilize_after_kick;
      else
        stay;
    }
    action 
    {
      situation.abortable = false;
      kick_ball(direction= attack.direction);

      head.motion_type = look_at_world_point;
      head.point_in_world.x = ball.percept.x;
      head.point_in_world.y = ball.percept.y;
      head.point_in_world.z = ball.radius;
    }
  }

  state stabilize_after_kick {
    decision
    {
      if ( state_time > 0 )
        goto finished;
      else
        stay;
    }
    action
    {
      situation.abortable = false;
      head.motion_type = look_at_world_point;
      head.point_in_world.x = ball.percept.x;
      head.point_in_world.y = ball.percept.y;
      head.point_in_world.z = ball.radius;
      motion.type = stand;
    }
  }

  target state finished
  {
    action
    {
    }
  }

}