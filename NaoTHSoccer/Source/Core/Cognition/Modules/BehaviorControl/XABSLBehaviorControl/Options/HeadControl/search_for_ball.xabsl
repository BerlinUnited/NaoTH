/** Scan mode for looking at the ball/searching for the ball */
option search_for_ball
{
  bool @force_ball_model_check; // false by default
  
  initial state start_search 
  {
    decision 
    {
	    if((ball.time_since_last_seen < 3000 || 
	       @force_ball_model_check) && 
	       abs(value=ball.angle) < 60)
	      goto try_to_look_at_the_old_ball;
	    else
	      goto search_for_ball;
	  }
	  action 
	  {
	  }
  }

  state try_to_look_at_the_old_ball
  {
    decision
    {
      if (state_time > 2000)
        goto search_for_ball;
      else
        stay;
    }
    action
    {
      head.camera.id = bottom;
      head.motion_type = look_at_world_point;
      head.point_in_world.x = ball.x;
      head.point_in_world.y = ball.y;
      head.point_in_world.z = ball.radius; // radius of the ball
    }
  }

  
  state search_for_ball
  {
    decision
    {
      if (ball.was_seen)
        goto look_at_ball_percept;
      else if( state_time > 3000 && 
        // there is still hope for the old model to be valid
        (ball.time_since_last_seen < 15000 || @force_ball_model_check) && 
        // ball model is reachable by moving my head only
        abs(value=ball.angle) < 60)
       goto try_to_look_at_the_old_ball;
      //else if( head.executed_motion_type != search )
       //goto search_for_ball_near;
      else
       stay;
    }
    action
    {
      head.camera.id = bottom;
      
      head.motion_type = look_at_point_on_the_ground;
      head.point_on_grond.x = attention.mi_point.x;
      head.point_on_grond.y = attention.mi_point.y;
    }
  }

  // TODO: does this state include try_to_look_at_the_old_ball?
  state search_for_ball_around_ball
  {
    decision
    {
      if (ball.was_seen)
        goto look_at_ball_percept;
      else if(state_time > 2000)
        goto search_for_ball;
      else
        stay;
    }
    action
    {
      head.motion_type = search;
      head.camera.id = bottom;
      head.search.center.x = ball.x;
      head.search.center.y = ball.y;
      head.search.center.z = ball.radius;
      head.search.size.x = 200;
      head.search.size.y = 200;
      head.search.size.z = 0;
    }
  }

  /*
  state search_for_ball_near
  {
    decision
    {
      if (ball.was_seen)
        goto look_at_ball_percept;
      else if(head.executed_motion_type != search)
        goto search_for_ball;
      else
        stay;
    }
    action
    {
      head.camera.id = bottom;
      head.motion_type = search;
      head.search.center.x = 800;
      head.search.center.y = 0;
      head.search.center.z = ball.radius;
      head.search.size.x = 500;
      head.search.size.y = 600;
      head.search.size.z = 0;
    }
  }
*/
  
  state look_at_ball
  {
    decision
    {
      if (ball.time_since_last_seen > 1000)
      {
        if(state_time > 1000)// I've seen the ball for a while => look at the old model
          goto search_for_ball_around_ball;
        else // I'just came here, but there is no ball => search for it
          goto search_for_ball;
      }
      else
        stay;
    }
    action
    {
      head.camera.id = bottom;
      head.motion_type = look_at_world_point;
      head.point_in_world.x = ball.x;//ball.percept.x;
      head.point_in_world.y = ball.y;//ball.percept.y;
      head.point_in_world.z = ball.radius; // radius of the ball
    }
  }


  state look_at_ball_percept
  {
    decision
    {
      if (state_time > 1000) // I waited long enough...
      {
        if(ball.time_since_last_seen < 100) // I still can see the ball => lok at the model
          goto look_at_ball;
        else
          goto search_for_ball; // I cannt see the ball anymore, must have been a mirage => go back, where I came from
      }
      else
        stay;
    }
    action
    {
      head.camera.id = bottom;
      head.motion_type = look_at_world_point;
      head.point_in_world.x = ball.percept.x;
      head.point_in_world.y = ball.percept.y;
      head.point_in_world.z = ball.radius; // radius of the ball
    }
  }
}
