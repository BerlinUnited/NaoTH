/** Side kick the ball to the right (with the left foot) in a 90 degree angle**/

option sidekick_to_right
{
  
  initial state sidestep_right
  {
    decision
    {
      if(state_time > 150 && executed_motion.step_control.left_movable)
      {
        goto sidekick_left_foot;
      }
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = walk;

      motion.walk_speed.x = ball.preview.left_foot.x - ball.radius - 100;
      motion.walk_speed.y = ball.preview.left_foot.y;
      motion.walk_speed.rot = 0;
    }
  }

  state sidekick_left_foot
  {
    decision
    {
      if(state_time > 100 && executed_motion.step_control.right_movable)
        goto reset_controls;
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = walk;
      
      motion.walk.coordinate = left_foot;
      motion.walk.style = normal;

      motion.walk.step_control.foot = left;
      motion.walk.step_control.target.x = 5*ball.preview.left_foot.x;
      motion.walk.step_control.target.y = 2*ball.preview.left_foot.y;
      motion.walk.step_control.target.rot = 0;
      motion.walk.step_control.speed_direction = -90;
      motion.walk.step_control.time = 200;
    }
  }

  state reset_controls
  {
    decision
    {
      if (state_time > 400)
        goto finished;
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = walk;
    
      motion.walk.coordinate = hip;
      motion.walk.style = normal;

      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;

      motion.walk.step_control.foot = none;
    }
  }

  target state finished
  {
    decision
    {
      stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = walk;
    
      motion.walk.coordinate = hip;
      motion.walk.style = normal;
  
      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;
    }
  }
}
