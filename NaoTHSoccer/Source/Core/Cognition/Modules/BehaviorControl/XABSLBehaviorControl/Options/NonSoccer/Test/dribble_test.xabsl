// test dribble with step control

option dribble_test {

  initial state search_ball {
    decision {
      if (ball.was_seen)
        goto go_to_ball;
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      head_control();
      
      motion.type = stand;
    }
  }

  state go_to_ball {
    decision {
      if(ball.time_since_last_seen > 1000)
      {
        goto search_ball;
      }
      else if(action_done)
       {
         goto kick_ball;
       }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      head_control();
      
      go_to_ball_omni(right_foot=true, distance=170);
    }
  }

  state kick_ball
  {
    decision {
      if(ball.time_since_last_seen > 1000)
      {
        goto search_ball;
      }
      else if(state_time > 2000)
      {
        goto go_to_ball;
      }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      head_control();

      motion.type = walk;
      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;
      motion.walk.coordinate = right_foot;
      motion.walk.step_control.foot = right;
      motion.walk.step_control.target.x = ball.right_foot.preview.x;
      motion.walk.step_control.target.y = ball.right_foot.preview.y;
      motion.walk.step_control.target.rot = 0;
      motion.walk.step_control.speed_direction = 0;
      motion.walk.step_control.time = 1000;
    }
  }
}