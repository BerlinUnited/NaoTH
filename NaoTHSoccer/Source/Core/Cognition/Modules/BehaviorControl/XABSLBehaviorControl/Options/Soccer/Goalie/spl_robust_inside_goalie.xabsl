option spl_robust_inside_goalie{

/*common decision
{
  if (ball.time_seen > 1000 && ball.distance < 1000)
    goto clear_ball;
}*/

state clear_ball {   
   decision {
       if (action_done 
           || (ball.distance > 1000 && ball.time_since_last_seen < 1000)
           || (ball.time_since_last_seen > 2000)) 
       {
         goto go_to_strategy_point;
       }
       else stay;
     }
     action {
       team.is_playing_as_striker = true;
       clear_ball_far_away();
     }
}
  
initial state go_to_strategy_point{
     decision {
       if (action_done)
        goto check_situation;
       else if (robot_pose.planned.x > -3000    && robot_pose.planned.x < -2600 &&
           robot_pose.planned.y <   500    && robot_pose.planned.y > -500 &&
           robot_pose.planned.rotation < 5 && robot_pose.planned.rotation > -5){
           goto check_situation;
      }else{
        stay;
      }
     }
     action {
      head.control_mode = goalie_look_around;
      go_to_absolute_point(rotation=0, x=-2800, y=0);
     }
}

state rotate_to_ball {
  decision {
     if (action_done || !ball.was_seen) {
       goto check_situation;
     } else {
        stay;
     }
  }
  action {
    head.control_mode = search_for_ball_force_model;
    go_to_relative_point(rotation=atan2(y=ball.preview.y, x=ball.preview.x), x=0, y=0);
  }
}

target state check_situation {
  decision {
   if (ball.time_since_last_seen < 1000 && abs(value=ball.angle) > 15) //20 degree caused by go_to_relative_point(rotation=ball.angle, x=0, y=0); in rotate____
       goto rotate_to_ball;
   else if ((ball.distance < 4000 && ball.time_since_last_seen < 1000)) {  
            // || (ball.team.position.abs() < 2000  && ball.team.time_since_last_update < 2000)) {
      goto critical_ball;
    }
  //only go to strategy point if the robot is localized wrong 
  // and is already searching around for more than 5 seconds
  //vorher: state_time < 5000 || vielleicht wichtig!?
  //ohne bewegung auf team.ball hoeren und gucken.
    else if (robot_pose.planned.x > -3200   && robot_pose.planned.x < -2400 &&
           robot_pose.planned.y <   700    && robot_pose.planned.y > -700 &&
           robot_pose.planned.rotation < 5 && robot_pose.planned.rotation > -5) 
    {
      stay;  
    }else{
      goto go_to_strategy_point;
    }
  }
  action {
    team.is_playing_as_striker = false;
    motion.type = stand;
    head.control_mode = goalie_look_around;
  }
}

state critical_ball {
  decision {
     if (action_done) {
       goto check_situation;
     } else {
        stay;
     }
  }
  action {
    critical_ball();
  }

}

}