/* If walking in a slight curve fails, read comment in the look for ball state */

option penalty_kicker
{
  initial state look_for_ball
  {
    decision
    {
      if(ball.was_seen && state_time > 3000)
        //Uncomment this if approaching fails: goto approach_ball
        //Comment the following to deactivate the slight approach curves
        if (random > 0.5)
          goto approach_ball_offset_right;
        else
          goto approach_ball_offset_left;
      else
        stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }


  state approach_ball_offset_left
  {
    decision
    {
      if (ball.preview.right_foot.x < 500)
        goto approach_ball;
      else
        stay;
    }
    action
    {
      motion.type = walk;
      motion.walk.style = stable;
      motion.walk.coordinate = right_foot;
      motion.walk_speed.x = 0.7*(ball.preview.right_foot.x - 500 - ball.radius);
      motion.walk_speed.y = ball.preview.right_foot.y + 250;
      motion.walk_speed.rot = 0.25*atan2(y = ball.preview.right_foot.y + 250, x = ball.preview.right_foot.x - 500);
      
      head.control_mode = search_for_ball;
    }
  }

  state approach_ball_offset_right
  {
    decision
    {
      if (ball.preview.right_foot.x < 500)
        goto approach_ball;
      else
        stay;
    }
    action
    {
      motion.type = walk;
      motion.walk.style = stable;
      motion.walk.coordinate = right_foot;
      motion.walk_speed.x = 0.7*(ball.preview.right_foot.x - 500 - ball.radius);
      motion.walk_speed.y = ball.preview.right_foot.y - 125;
      motion.walk_speed.rot = 0.25*atan2(y = ball.preview.right_foot.y - 125, x = ball.preview.right_foot.x - 500);
      
      head.control_mode = search_for_ball;
    }
  }

  state approach_ball
  {
    decision
    {
      if (abs(value = (ball.preview.right_foot.x - 120 - ball.radius)) < 15
        && abs(value = ball.preview.right_foot.y) < 10
        && atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) < 10)
        goto prepare_kick;
      else
        stay;
    }
    action
    {
      motion.type = walk;
      motion.walk.style = stable;
      motion.walk.coordinate = right_foot;
      motion.walk_speed.x = 0.5*(ball.preview.right_foot.x - 120 - ball.radius);
      motion.walk_speed.y = ball.preview.right_foot.y;
      motion.walk_speed.rot = atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x);
      
      head.control_mode = search_for_ball;
    }
  }

  state prepare_kick
  {
    decision
    {
      if(state_time > 2000)
      {
        if( abs(value = (ball.preview.right_foot.x - 120 - ball.radius)) > 25
          || abs(value = ball.preview.right_foot.y) > 25
          || atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) > 30)
          goto approach_ball;
        else
          goto wait;
      }
      else
        stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }

  state wait
  {
    decision
    {
      if (state_time > 6000)
        goto do_kick;
      else
        stay;
     }
     action
     {
     }
   }

  state do_kick
  {
    decision
    {
      if(action_done)
        goto finished;
      else
        stay;
    }
    action
    {
      situation.abortable = false;
      kick_ball_with_foot(direction = 0, right = true);
      head.control_mode = search_for_ball;
    }
  }


  target state finished
  {
    decision
    {
      stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = look_straight_ahead;
    }
  }

}