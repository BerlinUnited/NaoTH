option play_soccer
{
  common decision
  {
    if(!body.ready_to_walk)
      goto idle;
  }  

  initial state idle{
    decision{
      else if ( (game.current_mode == playing)||(game.current_mode == ready) )
        goto calibrate_before_ready_or_play;
      else if (game.current_mode == set)
        goto set;
      else
        stay;
    }
    action{}
  }

  state calibrate_before_ready_or_play
  {
    decision
    {
      else if((sensor.inertial.calibrated) && (game.current_mode == playing))
        goto play;
      else if((sensor.inertial.calibrated) && (game.current_mode == ready))
        goto ready;
      else if (!( (game.current_mode == playing)||(game.current_mode == ready) ))  
        goto idle;
      else 
        stay;
    }
    action
    {
      show_calibration();
      motion.type = stand;
      head.control_mode = look_straight_ahead;
    }
  
  }

  state play
  {
    decision
    {
      else if (game.current_mode == ready)
        goto ready;
      else if (game.current_mode == set)
        goto set;
      else if (!(game.current_mode == playing))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      decide_role();
    }
  }

  state ready
  {
    decision
    {
      else if (game.current_mode == playing)
        goto play;
      else if (game.current_mode == set)
        goto set;
      else if (!(game.current_mode == ready))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      decide_and_go_to_ready_position();
    }
  }

  state set
  {
    decision
    {
      else if (game.current_mode == playing)
        goto play;
      else if (game.current_mode == ready)
        goto ready;
      else if (!(game.current_mode == set))
        goto idle;
      else 
        stay;
    }
    action 
    {
      head.control_mode = search_for_ball;
    }
    
  }

  // from set to play: if would be striker -> be inital striker for e.i. 20 sec?
  
}