option play_soccer
{
  common decision
  {
    if(!body.ready_to_walk)
      goto idle;
  }  

  initial state idle{
    decision{
      else if (( (game.current_mode == playing)||(game.current_mode == ready) ))
        goto calibrate_before_ready_or_play;
      else
        stay;
    }
    action{}
  }

  state calibrate_before_ready_or_play
  {
    decision
    {
      else if((state_time>2000) && (game.current_mode == playing))
        goto play;
      else if((state_time>2000) && (game.current_mode == ready))
        goto ready;
      else if (!( (game.current_mode == playing)||(game.current_mode == ready) ))  
        goto idle;
      else 
        stay;
    }
    action
    {
    }
  
  }

  state play
  {
    decision
    {
      else if (game.current_mode == ready)
        goto ready;
      else if (!(game.current_mode == playing))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      decide_role();
    }
  }

  state ready
  {
    decision
    {
      else if (game.current_mode == playing)
        goto play;
      else if (!(game.current_mode == ready))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      decide_and_go_to_ready_position();
    }
  }
  
}