option play_soccer
{
  common decision
  {
    if(!body.ready_to_walk)
      goto idle;
  }  

  initial state idle{
    decision{
      else if ( (game.current_mode == playing)||(game.current_mode == ready) )
        goto calibrate_before_ready_or_play;
      else if (game.current_mode == set)
        goto set;
      else
        stay;
    }
    action{}
  }

  state calibrate_before_ready_or_play
  {
    decision
    {
      else if((sensor.inertial.calibrated) && (game.current_mode == playing))
        goto play;
      else if((sensor.inertial.calibrated) && (game.current_mode == ready))
        goto ready;
      else if (!( (game.current_mode == playing)||(game.current_mode == ready) ))  
        goto idle;
      else 
        stay;
    }
    action
    {
      show_calibration();
      motion.type = stand;
      head.control_mode = look_straight_ahead;
    }
  
  }

  // actualy check for localize
  state play
  {
    decision
    {
      else if(!last_state_was_penalized ||
        (robot_pose.is_valid && state_time>6000) || 
        state_time>12000)
        goto real_play;
      else
        stay;
    }
    action 
    {
      head.control_mode = search_for_goal;
      situationStatusOwnHalf = true;
      motion.type = stand;
    }
  }

  state real_play
  {
    decision
    {
      else if (game.current_mode == ready)
        goto ready;
      else if (game.current_mode == set)
        goto set;
      else if (!(game.current_mode == playing))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      last_state_was_penalized = false; // important!!!
      decide_role();
    }
  }


  state ready
  {
    decision
    {
      else if (game.current_mode == playing)
        goto play;
      else if (game.current_mode == set)
        goto set;
      else if (!(game.current_mode == ready))
        goto idle;
      else 
        stay;
    }
    action 
    {
      arm.control_mode = back;
      decide_and_go_to_ready_position();
      
      //only the first we know that we are in our own half
      situationStatusOwnHalf = (game.msecsRemaining >= 600000);
    }
  }

  state set
  {
    decision
    {
      else if (game.current_mode == playing && team.calc_if_is_striker)
        goto initial_striker;
      else if (game.current_mode == playing)
        goto play;
      else if (game.current_mode == ready)
        goto ready;
      else if (!(game.current_mode == set))
        goto idle;
      else 
        stay;
    }
    action 
    {
      head.control_mode = search_for_ball;
      situationStatusOwnHalf = true;
    }
    
  }

  state initial_striker {
	  decision {
	    else if (game.current_mode == ready)
        goto ready;
      else if (game.current_mode == set)
        goto set;
      else if (!(game.current_mode == playing))
        goto idle;
	    else if(team.calc_if_is_striker)
	      stay;  
	    else
	      goto play;
	  }
	  action {
	    initial_striker();
	  }
  }

  // from set to play: if would be striker -> be inital striker for e.i. 20 sec?
  
}