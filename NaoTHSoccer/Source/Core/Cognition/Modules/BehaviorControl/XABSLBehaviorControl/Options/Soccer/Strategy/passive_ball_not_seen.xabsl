option passive_ball_not_seen
{

  common decision 
  {
    if(ball.was_seen)
      goto turn_to_ball;
  }

  initial state look_around
  {
    decision {
      else if(state_time > 10000)
          goto turn_to_opponent_direction;
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state turn_to_opponent_direction {
    decision {
      else if(abs(value=(0-robot_pose.planned.rotation))<10)
        goto look_around_2;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot= abs(value=(0-robot_pose.planned.rotation))>10?(0-robot_pose.planned.rotation):0;
      head.control_mode = search_for_ball;      
    }
  }

  state look_around_2{
    decision {
      else if(state_time > 10000)
        //goto search_far;
          goto decide_turn_direction;
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state decide_turn_direction{
    decision{
      else if(robot_pose.planned.rotation < 0)
        goto turn_right_to_own_direction;
      else
        goto turn_left_to_own_direction;
    }
    action{
    }
  }

  state turn_left_to_own_direction {
    decision {
      else if(abs(value=(180-robot_pose.planned.rotation))<10 )
        goto look_around;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot= abs(value=(180-robot_pose.planned.rotation))>10?(180-robot_pose.planned.rotation):0;
      head.control_mode = search_for_ball;      
    }
  }

   state turn_right_to_own_direction {
    decision {
      else if(abs(value=(-180-robot_pose.planned.rotation))<10 )
        goto look_around;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot= abs(value=(-180-robot_pose.planned.rotation))>10?(-180-robot_pose.planned.rotation):0;
      head.control_mode = search_for_ball;      
    }
  }

  target state turn_to_ball 
  {
    decision {
      else if(ball.time_since_last_seen > 1000)
        goto look_around;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot=atan2(y=ball.preview.y, x=ball.preview.x);
      head.control_mode = search_for_ball;
    }
  }
}