/** Go straight to the ball with respect to hip coordinates 
  Note: No rotation with less then 20 cm distance! **/

option go_to_ball
{
  initial state go_to_ball
  {
    decision
    {
      if (abs(value = (ball.percept.x - 100 - ball.radius)) < 20
        && abs(value = ball.percept.y) < 20
        && abs(value = atan2(y = ball.percept.y, x = ball.percept.x)) < 20
        && vector.abs(x=ball.speed.x, y=ball.speed.y) < 12)
        goto go_to_ball_done;
      else
        stay;
    }
    action 
    {
      head.control_mode = search_for_ball;
      motion.type = walk;
      
      motion.walk.coordinate = hip;
      motion.walk.style = normal;
      motion.walk_speed.x = 0.7*(ball.preview.x - 100 - ball.radius);
      motion.walk_speed.y = ball.preview.y;
      motion.walk_speed.rot = (ball.distance > 200) ? atan2(y = ball.preview.y, x = ball.preview.x) : 0;
    }
  }

  target state go_to_ball_done
  {
    decision
    {
      if (abs(value = (ball.percept.x - 100 - ball.radius)) > 30
        || abs(value = ball.percept.y) > 30
        || abs(value = atan2(y = ball.percept.y, x = ball.percept.x)) > 30
        || vector.abs(x=ball.speed.x, y=ball.speed.y) > 17)
        goto go_to_ball;
      else
        stay;
    }
    action 
    {
      head.control_mode = search_for_ball;
      motion.type = walk;
      motion.walk.coordinate = hip;
      motion.walk.style = normal;

      motion.walk_speed.x = (ball.preview.x - 100 - ball.radius)>0 ? 0.7*(ball.preview.x - 100 - ball.radius) : 0;
      motion.walk_speed.y = ball.preview.y;
      motion.walk_speed.rot = (ball.distance > 200) ? atan2(y = ball.preview.y, x = ball.preview.x) : 0;
    }
  }
}
