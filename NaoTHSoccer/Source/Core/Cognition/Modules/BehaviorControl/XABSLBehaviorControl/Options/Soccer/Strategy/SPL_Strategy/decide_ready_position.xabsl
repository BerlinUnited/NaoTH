option decide_ready_position
{
  common decision {
    if(fall_down_state != upright)
      goto stand_up;
  }
  
  initial state check_out_the_situation
  {
    decision
    {
      else if(state_time < 6000)
        stay;
      else if(game.own_kickoff)
        goto own_kickoff;
      else
        goto opp_kickoff;
    }
    action
    {
      head.control_mode = look_around;
      motion.type = stand;
      
      //only the first we know that we are in our own half
      situationStatusOwnHalf = (game.msecsRemaining>=600000) ? true:false;
    }
  }

  state own_kickoff{
    action{
      situationStatusOwnHalf = false; 
      decide_offense_position();
    }
  }

  state opp_kickoff{
    action{
      situationStatusOwnHalf = false; 
      decide_defense_position();
    }
  }
  state stand_up
  {
    decision
    {
      else if(action_done)
        goto calibrate;
      else
        stay;
    }
    action 
    {
      stand_up();
    }
  }

  state calibrate
  {
    decision
    {
      else if(action_done)
        goto check_out_the_situation;
      else
        stay;
    }
    action
    {
      calibrate_intertial();
    }
  }

  target state stand {
    action {
      head.control_mode = look_around;
      motion.type = stand;
    }
  }
}