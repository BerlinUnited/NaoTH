/** Playing as striker */
option sim_playing_striker {

  initial state ball_not_seen {
    decision {
      if (ball.was_seen)
        goto dribble;
      else
        stay;
    }
    action {
      simple_search_ball();
      team.is_playing_as_striker = false;
    }
  }

  state dribble {
    decision {
      if(ball.time_since_last_seen > 2000)
      {
        goto ball_not_seen;
      }
      else if ( players.own_closest_to_ball.number > 0.1 &&
        abs(value=players.own_closest_to_ball.number - game.player_number) > 0.1
        && players.own_closest_to_ball.distance_to_ball < 400 
        && ball.distance < 1000 )
      {
        // the teammates is very close to ball!!
        goto stand_by;
      }
      //HACK: the kick is too weak
      else if ( goal.opp.distance < 3000 && goal.opp.distance > 1500 && abs(value=goal.opp.angle) < 30)
      {
        goto shoot;
      }
      else
        stay;
    }
    action {
      dribble(direction=attack.direction);
      head.control_mode = search_for_ball;
      team.is_playing_as_striker = true;
    }
  }
  
  state shoot {
    decision {
      if(ball.time_since_last_seen > 2000)
      {
        goto ball_not_seen;
      }
      else if ( goal.opp.distance > 3500 || goal.opp.distance < 1000 || abs(value=goal.opp.angle) > 45)
      {
        goto dribble;
      }
      else
        stay;
    }
    action {
      shoot();
      team.is_playing_as_striker = true;
    }
  }

  state stand_by {
    decision {
      if ( ball.time_since_last_seen > 2000 )
      {
        goto ball_not_seen;
      }
      else if ( players.own_closest_to_ball.number < 0.1
        || abs(value=players.own_closest_to_ball.number - game.player_number) < 0.1
        || players.own_closest_to_ball.distance_to_ball > 500 
        || ball.distance > 1000 )
        {
          goto dribble;
        }
      else
        stay;

    }
    action {
      //motion.type = stand;
      move_around_ball(direction=attack.direction, radius = 800);
      head.control_mode = smart_search;
      team.is_playing_as_striker = true;
    }
  }
}