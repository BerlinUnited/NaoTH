/** Side kick the ball to the left (with the right foot) in a 90 degree angle
    assumes ball is infront of hip instead of foot **/

option sidekick_to_left
{
  
  initial state sidestep_left
  {
    decision
    {
      if(state_time > 250 && executed_motion.step_control.right_movable)
      {
        goto sidekick_right_foot;
      }
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.type = walk;

      motion.walk_speed.x = ball.preview.right_foot.x - ball.radius - 130;
      motion.walk_speed.y = ball.preview.right_foot.y;
      motion.walk_speed.rot = 0;
    }
  }

  state sidekick_right_foot
  {
    decision
    {
      if(state_time > 100 && executed_motion.step_control.left_movable)
        goto turn_left;
      else
        stay;
    }
    action
    {
      motion.type = walk;
      
      motion.walk.coordinate = right_foot;
      motion.walk.style = normal;

      motion.walk.step_control.foot = right;
      motion.walk.step_control.target.x = ball.preview.right_foot.x + 500;
      motion.walk.step_control.target.y = ball.preview.right_foot.y;
      motion.walk.step_control.target.rot = 0;
      motion.walk.step_control.speed_direction = 90;
      motion.walk.step_control.time = 300;

      //loook at percept
      head.control_mode = none;
      head.control_mode = look_at_ball_percept;
    }
  }

  state turn_left {
	  decision {
      if(state_time > 500)
        goto reset_controls;
      else 
        stay;
	  }
	  action {
      motion.type = walk;
      motion.walk.style = normal;
      motion.walk.coordinate = hip;
      motion.walk.step_control.foot = none;
      motion.walk_speed.rot=18;

      head.control_mode = look_at_ball_percept;
	  }
	}

  state reset_controls
  {
    decision
    {
      if (state_time > 500) //uerspruenglich 1000
        goto finished;
      else
        stay;
    }
    action
    {
      motion.type = stand;
    
      motion.walk.coordinate = hip;
      motion.walk.style = normal;

      motion.walk.step_control.foot = none;

      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }

  target state finished
  {
	  decision
	  {
      stay;
	  }
	  action
	  {
	    motion.type = stand;
    
      motion.walk.coordinate = hip;
      motion.walk.style = normal;

      //loook at percept
      head.control_mode = look_at_ball_percept;
	  }
  }
}
