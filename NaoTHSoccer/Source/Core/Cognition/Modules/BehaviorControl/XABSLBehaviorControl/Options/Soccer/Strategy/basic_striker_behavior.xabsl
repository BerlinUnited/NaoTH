option basic_striker_behavior{

 common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if((ball.time_since_last_seen>1000)&&(vector.abs(y=ball.preview.y,x=ball.preview.x)<250))
      goto search_ball_close;
    else if((ball.time_since_last_seen>1000)&&(vector.abs(y=ball.preview.y,x=ball.preview.x)>250))
      goto search_ball_far;
  }
  
  initial state idle {
    decision {
      else if(game.current_mode == playing)
        goto search_ball_close;
      else
        stay;
    }
    action {
      
    }
  }
  
  state search_ball_close {
    decision {
      else if(action_done)
        goto search_ball_far;
      else
        stay;
    }
    action {
      search_ball_back_rot();
    }
  }

  state search_ball_far {
    decision {
      else if(action_done)
        goto go_to_ball;
      else
        stay;
    }
    action {
      simple_search_ball();
    }
  }

  state go_to_ball {
    decision {
      else if(action_done)
        goto decide_turning_direction;
      else
        stay;
    }
    action {
      go_to_ball(distance=130);
    }
  }

  state decide_turning_direction {
    decision{
      else if(abs(value=attack.direction.preview)<10)
        goto attack;
      else if(attack.direction.preview>0)
        goto turn_right_around;
      else
        goto turn_left_around;
    }
    action{
    }

  }

  state turn_right_around {
    decision {
      else if(abs(value=attack.direction.preview)<10)
        goto attack;
      else
        stay;
    }
    action {
      move_around_ball(direction=30, radius=vector.abs(x=ball.preview.x, y=ball.preview.y));
    }
  }

  state turn_left_around {
    decision {
      else if(abs(value=attack.direction.preview)<10)
        goto attack;
      else
        stay;
    }
    action {
      move_around_ball(direction=-30, radius=vector.abs(x=ball.preview.x, y=ball.preview.y));
    }
  }


  state attack {
    decision {
      else if(state_time>1000)
        goto go_to_ball;
      else
        stay;
    }
    action {
      motion.type = walk;
      motion.walk.style = fast;
      motion.walk_speed.x=ball.preview.x+100;
      motion.walk_speed.y=ball.preview.y;
      motion.walk_speed.rot=0;

      head.control_mode = search_for_ball;
    }
  }
}
