option basic_striker_behavior{

  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
  }

  // this is only usefull for test agents which call basic_striker_behavior directly
  initial state idle {
    decision {
      else if(game.current_mode == playing)
        goto decide_search;
      else
        stay;
    }
    action {
    }
  }

  state decide_search{
    decision{
      else if (ball.was_seen || ball.time_since_last_seen < 100)
        goto go_to_ball;
      else
        stay;
    }
    action{
      search_ball();
    }
  
  }

  state go_to_ball {
    decision {
      else if (ball.time_since_last_seen>1500)
        goto decide_search;
      else if(action_done)
        goto check_for_obstacles;
      else
        stay;
    }
    action {
      go_to_ball_with_USOA(distance=190);
    }
  }

  state check_for_obstacles {
	   decision {
      else if((obstacle.ultrasound.distance < 350) && (obstacle.ultrasound.blockedtime > 200))
        goto close_to_ball_with_USOA_situation_state;
      else
        goto decide_turning_direction;
	   }
	   action {
	   }
	 }

  state close_to_ball_with_USOA_situation_state {
	   decision {
      else if (ball.time_since_last_seen>1500)
        goto decide_search;
      else if(action_done)
        goto go_to_ball;
      else
        stay;
	   }
	   action {
      close_to_ball_with_USOA_situation();
	   }
	 }

  state decide_turning_direction {
    decision{
      else if (ball.time_since_last_seen>1500)
        goto decide_search;
      else if (obstacle.ultrasound.blockedtime > 200 && obstacle.ultrasound.distance < 350  && state_time > 500)
        goto check_for_obstacles;
      else if((abs(value=attack.direction.preview)<20 && robot_pose.planned.x<3500) || abs(value=attack.direction.preview)<12)
        goto preattack;
      else if(( (abs(value=robot_pose.planned.x)<4000) && abs(value=attack.direction.preview)>90)
              ||(abs(value=attack.direction.preview)<90 && abs(value=attack.direction.preview)>40))
        goto sidekick_with_foot;
      else if(vector.abs(x=ball.preview.x, y=ball.preview.y)>250)
        goto go_to_ball;
      else
        stay;
    }
    action{
      move_around_ball(direction=(attack.direction.preview>0)?30:-30, radius=190);
      motion.walk.style = fast;
    }
  }


  state preattack {
    decision{
      else if (obstacle.ultrasound.blockedtime > 200 && obstacle.ultrasound.distance < 350)
        goto check_for_obstacles;
      else if (ball.time_since_last_seen>1500)
        goto decide_search;
      else if (action_done) 
      {
	      if(random < 0.3)
	        goto do_kick;
	      else
	        goto attack;
      }
      else 
        stay;
    }
    action{
      go_to_ball_with_foot(right=true, distance=190);
      motion.walk.style = normal;
    }
  }
 
  //dash
  state attack {
    decision {
      else if(action_done)
        goto go_to_ball_for_a_second;
      else
        stay;
    }
    action {
      //close_to_ball_with_USOA_situation();
      fast_forward_kick_with_right();
    }
  }

  state approach_for_kick{
    decision{
      else if(abs(value=attack.direction.preview) >= 20 && ball.preview.y < 100)
          goto go_to_ball;
      else if(action_done)
          goto do_kick;
      else
        stay;
    }
    action
    {
      go_to_ball_with_foot(right=true, distance=190);
    }
  }

  state do_kick {
    decision {
      else if(action_done)
        goto go_to_ball_for_a_second;
      else
        stay;
    }
    action {
      kick_with_foot(right = true);
    }
  }

  state sidekick_with_foot{
    decision{
      else if(action_done)
        goto go_to_ball_for_a_second;
      else
        stay;
    }
    action{
      sidekick(to_right=attack.direction.preview<0);
      
      motion.walk.style = normal;
      //motion.walk.style = stable;
    }
  }

  // unsatisfying solution for the second kick problem
  state go_to_ball_for_a_second{
    decision {
      else if (ball.time_since_last_seen>1500)
        goto decide_search;
      else if(state_time > 1000)
        goto go_to_ball;
      else
        stay;
    }
    action {
      go_to_ball_with_USOA(distance=190);
    }
  }
}
