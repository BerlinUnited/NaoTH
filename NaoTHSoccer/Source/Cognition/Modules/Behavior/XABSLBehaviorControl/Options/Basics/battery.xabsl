/** The show battery status and warn if too low */
option battery {

  initial state ok {
    decision 
    {
      if(battery.isCharging)
        goto slow_blink;
      else if(platform.battery <= 0.05)
        goto low_warn;
      else if((!battery.isCharging) && (!battery.isDischarging) && platform.battery>=0.96)
        goto slow_blink;
      else if((!battery.isCharging) && (!battery.isDischarging) && platform.battery<0.96)
        goto fast_blink;
      else
        stay;
    }
    action {
      set_left_ear = min(value0=10.0,value1=platform.battery*10);
      set_right_ear = min(value0=10.0,value1=platform.battery*10);
    }
  }

  state low_warn
  {
    decision
    {
      if(battery.isCharging)
        goto slow_blink;
      else if(platform.battery > 0.05)
        goto ok;
      else
        stay;
    }
    action
    {
      set_left_ear = 0;
      set_right_ear = 0;
    }
  }
  
  state fast_blink
  {
    decision
    {
      if(battery.isCharging || battery.isDischarging || platform.battery>=0.96)
        goto ok;
      else
        stay;        
    }
    action
    {
      //Fast flashing blinking
      set_left_ear = (platform.frameNumber % 8 < 4) ? 9 : 0;
      set_right_ear = (platform.frameNumber % 8 < 4) ? 9 : 0;

    }
  }

  state slow_blink
  {
    decision
    {
      if ((!battery.isCharging) && (!battery.isDischarging) && platform.battery>=0.96)
        stay;
      else if(!battery.isCharging && (battery.isDischarging))
        goto ok;
      else
        stay;
    }
    action
    {
      set_left_ear = (platform.frameNumber % 30 < 15) ? 9 : 0;
      set_right_ear = (platform.frameNumber % 30 < 15) ? 9 : 0;
      set_head = (platform.frameNumber % 30 < 15) ? ((robot_pose.x < 0) ? 3 : 6) : 0;
    }
  }
}
// end option
