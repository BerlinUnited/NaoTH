option arms_control
{
  initial state arms_normal
  {
    decision {
      if(arm.control_mode == back)
        goto arms_back;
      else if(arm.control_mode == synchronised_with_walk)
        goto arms_synchronised_with_walk;
      else if((body.collision.arm.left || body.collision.arm.right) && arm.react_to_collision)
        goto arms_back_by_collision;
      else
        stay;
    }
    action {
      arm.type = arms_down;
    }
  }

  state arms_back
  {
    decision {
      if(arm.control_mode != back)
        goto arms_take_down;
      else
        stay;
    }
    action {
      arm.type = arms_back;
    }
  }

  state arms_back_by_collision
  {
    decision {
      if(state_time > 5000)
        goto arms_take_down;
      else if(arm.control_mode == back)
        goto arms_back;
      else if(!arm.react_to_collision)
        goto arms_normal;
      else
        stay;
    }
    action {
      arm.type = arms_back;
    }
  }

  state arms_synchronised_with_walk
  {
    decision {
      if(arm.control_mode != synchronised_with_walk)
        goto arms_take_down;
      else if((body.collision.arm.left || body.collision.arm.right) && arm.react_to_collision)
        goto arms_back_by_collision;
      else
        stay;
    }
    action {
      arm.type = arms_synchronised_with_walk;
    }
  }

  state arms_take_down
  {
    decision {
      if(state_time > 1000)
        goto arms_normal;
      else
        stay;
    }
    action {
      arm.type = arms_down;
    }
  }
}
