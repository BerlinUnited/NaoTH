option role_goalie
{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
  }

  initial state idle {
    decision {
      // only in PLAY switch state, otherwise stay (see common decision)
      else if(game.current_mode == playing)
        goto go_to_strategy_point;
      else
        stay;
    }
    action {}
  }

  state go_to_strategy_point
  {
     decision 
     {
       else if(strategy.role_dynamic == striker)
         goto clear_ball;
       else if (action_done)
         goto stand_and_look_at_ball;
       // goalie is "behind" the goal
       else if (abs(value = robot_pose.planned.y) > (field.yPosLeftGoal + 50) && robot_pose.planned.x < (field.xPosOwnGroundline+50))
        goto return_to_field;
       else 
         stay;
     }
     action {
       go_to_point_with_USOA(
         x=strategy.position.home.x,
         y=strategy.position.home.y,
         rot=0);
       head.control_mode = look_around;
     }
  }

  state go_to_strategy_point_search_for_ball
  {
     decision 
     {
       else if(strategy.role_dynamic == striker)
         goto clear_ball;
       else if (action_done)
         goto stand_and_look_at_ball;
       // goalie is "behind" the goal
       else if (abs(value = robot_pose.planned.y) > (field.yPosLeftGoal + 50) && robot_pose.planned.x < (field.xPosOwnGroundline+50))
        goto return_to_field;
       else 
         stay;
     }
     action {
       go_to_point_with_USOA(
         x=strategy.position.home.x,
         y=strategy.position.home.y,
         rot=0
         );
      head.control_mode = search_for_ball_goalie;
     }
  }

  // if the goalie knows where he is and he's somewhere "behind" the goal,
  // he should first return the the field before going back to his home position (avoid running into the goal net)
  // NOTE: this should be part of a path planner!
  state return_to_field {
    decision {
      else if(strategy.role_dynamic == striker)
        goto clear_ball;
      else if (abs(value = robot_pose.planned.y) <= (field.yPosLeftGoal + 100) || robot_pose.planned.x >= (field.xPosOwnGroundline+100))
        goto go_to_strategy_point;
      else
        stay;
    }
    action {
      // use 300 to make sure, the goalie is back in the field -> go_to_point proximity!
       go_to_point_with_USOA(
         x=field.xPosOwnGroundline+300,
         y=robot_pose.planned.y < 0 ? (field.yPosRightGoal - 300) : (field.yPosLeftGoal + 300),
         rot=robot_pose.planned.y < 0 ? 90 : -90);
       head.control_mode = look_around;
    }
  }

  state stand_and_look_at_ball {
    decision {
      else if(strategy.role_dynamic == striker)
        goto clear_ball;
      else if(vector.abs(x=locator.field_to_relative.x(x=strategy.position.home.x, y=strategy.position.home.y), y=locator.field_to_relative.y(x=strategy.position.home.x, y=strategy.position.home.y)) >= 300)
        goto go_to_strategy_point_search_for_ball;
      else 
        stay;
    }
    action {
      motion.type = stand;
      head.control_mode = search_for_ball_goalie;
    }
  }

  state clear_ball {   
    decision {
      else if(strategy.role_dynamic != striker) {
         goto go_to_strategy_point;
      }
      else stay;
    }
    action {
      team.is_playing_as_striker = true;
      path_striker2018();
    }
  }
}