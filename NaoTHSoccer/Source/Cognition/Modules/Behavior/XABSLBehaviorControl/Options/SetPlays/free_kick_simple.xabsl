/**
 * This is the simplest free-kick and kick-in behavior.
 * If its our free-kick, we continue our normal play;
 * Otherwise we search for the ball and if we found the ball, we move back from it.
 */
option free_kick_simple {

  /** true if own free kick **/
  bool @own;
  
  initial state decide
  {
    decision {
      if(@own)
        goto own_free_kick;
      else
        if (!ball.know_where_itis)
          if (ball.preview.y > 0)
            goto opp_free_kick_search_ball_left;
          else
            goto opp_free_kick_search_ball_right;
        else
          goto opp_free_kick_turn_to_ball;
    }
    action {
    }
  }
  
  state opp_free_kick_search_ball_left
  {
    decision {
      if (ball.know_where_itis)
        goto opp_free_kick_turn_to_ball;
      else
        stay;
    }
    action {
      search_ball_turning(search_direction = left);
    }
  }

  state opp_free_kick_search_ball_right
  {
    decision {
      if (ball.know_where_itis)
        goto opp_free_kick_turn_to_ball;
      else
        stay;
    }
    action {
      search_ball_turning(search_direction = right);
    }
  }

  state opp_free_kick_turn_to_ball
  {
    decision {
      if (abs(value = atan2(y = ball.preview.y, x = ball.preview.x)) < 10)
        goto opp_free_kick_move_away;
      else
        stay;
    }
    
    action {
      head.control_mode = search_for_ball;
      
      motion.type = walk;
      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = atan2(y = ball.preview.y, x = ball.preview.x);
    }
  }

  state opp_free_kick_move_away
  {
    decision {
      // TODO: input symbol for self-loc security margin
      if (ball.preview.x > field.centerCircleRadius + 300) // self-loc security margin
        goto opp_free_kick_watch_ball;
      else
        stay;
    }
    
    action {
      head.control_mode = search_for_ball;

      motion.type = walk;
      motion.walk_speed.x = -100;
    }
  }

  state opp_free_kick_watch_ball
  {
    decision
    {
      stay;
    }
    
    action
    {
      head.control_mode = search_for_ball;

      motion.type = stand;
    }
  }

  state own_free_kick
  {
    decision {
      stay;
    }
    
    action {
        roles_decide_role();
    }
  }
}
