option decide_and_go_to_passive_position
{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
  }

  initial state idle {
	  decision {
      else if (action_done || state_time > 10000) // search for maximum 10s
        goto check_player_number;
      else
        stay;
	  }
	  action {
      search_ball();
      motion.walk.style = stable;
	  }
	}  
  
  state check_player_number
  {
    decision{
      else if(game.player_number == 2)
          goto goto_passive_position_supporter2;
      else if(game.player_number == 3)
          goto goto_passive_position_striker;
      else if(game.player_number == 4)
          goto goto_passive_position_defender;          
      else if(game.player_number == 5)
          goto goto_passive_position_supporter;
      else
          stay;
    }
    action {
      head.control_mode = look_around;
      motion.type = stand;
    }
  }

  // 3
  state goto_passive_position_striker {
    decision {
      else
        stay;
    }
    action {
      go_to_and_keep_passive_position(
        x  =(field.xPosOppPenaltyMark + field.centerCircleRadius)/2 + 500,
        y = field.yPosLeftGoal, 
        rot = 140);
    }
  }

  // 5
  state goto_passive_position_supporter {
    decision {
      else
        stay;
    }
    action {
      go_to_and_keep_passive_position(
        x = 1000,
        y = -field.centerCircleRadius, 
        rot = 45);
    }
  }
 
  // 4
  state goto_passive_position_defender {
    decision {
      else
        stay;
    }
    action {
      go_to_and_keep_passive_position(
        x=field.xPosOwnPenaltyMark, 
        y=field.yPosRightGoal+400, 
        rot=0);
    }
  }
 
  // 2
  state goto_passive_position_supporter2{
    decision {
      else
        stay;
    }
    action {
      go_to_and_keep_passive_position(
        x=field.xPosOwnPenaltyMark+1000, 
        y=field.yPosLeftGoal-400,
        rot=0);
    }
  }
}