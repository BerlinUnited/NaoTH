/** Side kick the ball to the right (with the left foot) in a 90 degree angle
    assumes ball is infront of hip instead of foot **/

option path_sidekick
{
  enum direction @to;
  
  initial state direction
  {
    decision
    {
      /* don't get stuck if run when in stand */
      if(executed_motion.type != walk)
        stay;
      /* check if the kicking foot was support foot before kicking,
         if not make a step before kicking so that the CoM can handle the kick with the foot*/
      else if(executed_motion.step_control.right_movable && @to == right)
        goto sidekick_left_foot;
      else if (executed_motion.step_control.left_movable && @to == left)
        goto sidekick_right_foot;
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.walk.style = normal;

      motion.type = walk;
      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;
    }
  }

  state sidekick_left_foot
  {
    decision
    {
      if( path.kick_executed )
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path.routine = sidekick_right;
    }
  }


  state sidekick_right_foot
  {
    decision
    {
      if( path.kick_executed )
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path.routine = sidekick_left;
    }
  }

  state short_freeze {
    decision {
      if(state_time > 500)
        goto finished;
      else 
        stay; 
    }
    action {
      path.routine = none;
    }
  }

  target state finished
  {
    decision
    {
      stay;
    }
    action
    {
      path.routine = none;

      head.control_mode = look_at_ball_percept;
    }
  }
}