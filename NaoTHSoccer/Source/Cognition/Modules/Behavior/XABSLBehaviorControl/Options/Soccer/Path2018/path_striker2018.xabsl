option path_striker2018
{

  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    /*
    // NOTE: this is only important for the old behavior (2018)
    else if(!game.state.playing_is_set_by_game_controller &&
           !game.own_kickoff && game.time_in_play < 10000)
      // TODO: for the roles, the initial striker isn't neccessary anymore and can be removed! (the behavior must be applied for all roles!)
      // if we see the ball outside the centercircle, the ball is free!
      if((ball.know_where_itis && (abs(value=ball.position.field.x) <= (300) && abs(value=ball.position.field.y) <= (300))) || !ball.know_where_itis)
        goto wait_for_ball_free;
      else
        goto goto_ball;
    */
    else if(situation_abortable && !ball.know_where_itis)//ball.time_since_last_seen > 1500)
      goto search;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 300)
      goto goto_ball;
  }

  state wait_for_ball_free {
    decision {
      else
        goto search;
    }
    action {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }

  initial state idle 
  {
    decision 
    {
      else
        goto search;
    }
    action 
    {
      situation_abortable = true;
      team.is_playing_as_striker = false;
      
      path2018.routine = none;
    }
  }

  state search 
  {
    decision 
    {
      else if(action_done)
        goto goto_ball;
      else
        stay;
    }
    action
    {
      situation_abortable = true;
      search_ball();
    }
  }

  state goto_ball {
    decision{

      else if(ball.preview.x < 300 && abs(value = ball.preview.y) < 50)
      {
        if(attack.best_action == kick_short) {
          if(random() > 0.0)
          {
            goto forwardkick;
          }else{
            goto dash;
          }
        }
        else if(attack.best_action == dribble_kick)
          goto dribble_kick;
        else if(attack.best_action == sidekick_left)
          //goto sidekick_left;
          goto turn;
        else if(attack.best_action == sidekick_right)
          //goto sidekick_right;
          goto turn;
        else
          goto turn;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      /* this distance is smaller than the condition for ball.preview.x above
         possible unwanted behavior: the ball is closer than 300 but ball.preview.y is
         bigger than 50. The robot won't rotate in go_to_ball_with_USOA and will continue
         to walk to the position of the ball. Two possible outcomes:
         1. the robot runs into the ball (if ball distance is not considered)
         2. the robot will walk backwards in the successive approaching because it was too close*/
      go_to_ball_with_USOA(distance = 210); 
    }
  }

  state turn
  {
    decision
    {
    // long kick is ignored/not included in action selection
    //else if(attack.best_action == kick_long)
    //    goto long_kick;
    else if(attack.best_action == kick_short)
    {
      if(random() > 0.0)
      {
        goto forwardkick;
      }
      else
        goto dash;
      
    }
    else if(attack.best_action == dribble_kick)
          goto dribble_kick;
    else if(attack.best_action == sidekick_left)
      //goto sidekick_left;
      stay;
    else if(attack.best_action == sidekick_right)
      //goto sidekick_right;
      stay;
    else
      stay;
    }
    action
    {
      situation_abortable = true;
      
      // TODO: path_turn_to_attack_direction() should be moved to a new option
      // PathPlanner2018 stuff was hacked in there
      path_turn_to_attack_direction();
    }
  }
  state dribble_kick
  {
    decision
    {
      else if (path.kick_executed && ball.percept.x > 200)
        goto walk_a_little;
      else
        stay;
    }
    action
    {
      path2018.routine = dribble_kick;
      path.xOffset = -120; // pathplanner adds ball.radius - this could be done here
      path.yOffset = 0;
      
      head.control_mode = search_for_ball;
    }
  
  }

  state forwardkick
  {
    decision
    {
      else if (path.kick_executed)
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path2018.routine = forwardkick;
      path.xOffset = 120; // pathplanner adds ball.radius - this could be done here
      path.yOffset = 0;
      head.control_mode = search_for_ball;
    }
  }
  
  state sidekick_right
  {
    decision
    {
      else if (path.kick_executed)
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path2018.routine = sidekick_right;
      head.control_mode = search_for_ball;
    }
  }
  
  state sidekick_left
  {
    decision
    {
      else if (path.kick_executed)
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path2018.routine = sidekick_left;
      head.control_mode = search_for_ball;
    }
  }

  state walk_a_little
  {
    decision
    {
      else if (state_time > 100)
        goto goto_ball;
      else
        stay;
    }
    action
    {
      motion.type = walk;
      motion.walk.style = fast;
      motion.walk_speed.rot = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.x = 100;
      motion.walk.coordinate = hip;
    }
  }
  state short_freeze
  {
    decision
    {
      else if (state_time > 500)
        goto goto_ball;
      else
        stay;
    }
    action
    {
      path2018.routine = none;
      
      situation_abortable = false;
      
      //look at percept
      head.control_mode = look_at_ball_percept;
    }
  }

  state dash 
  {
    decision
    {
      else if(action_done)
        goto goto_ball;
      else
        stay;
    }
    action
    {
      situation_abortable = true;
      path2018_dash();
    }
  }
}
