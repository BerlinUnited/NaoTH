option passive_ball_not_seen
{

  common decision 
  {
    if(ball.know_where_itis)
      goto turn_to_ball;
  }

  initial state look_around
  {
    decision {
      else if(state_time > 1000){
            if (ball.team.time_since_last_update < 5000)
              goto look_at_team_ball;
            else {
              // TODO: using ball.team.rmse might be beneficial
              if(atan2(y=ball.preview.y,x=ball.preview.x) < 0)
		            goto turn_to_right;
		          else
		            goto turn_to_left;
            }
      }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state look_at_team_ball
  {
	  decision
	  {
      else if(abs(value = atan2(y=ball.team.position.y, x=ball.team.position.x)) < 50){
        // turn in same direction if the ball wasn't seen
        if(atan2(y=ball.team.position.y, x=ball.team.position.x) > 0)
          goto turn_to_left;
        else
          goto turn_to_right;
      } else 
        stay;
	  }
	  
	  action
	  {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot=atan2(y=ball.team.position.y, x=ball.team.position.x);
      head.control_mode = look_at_team_ball;
	  }
  }

  state turn_to_left {
    decision {
      else if(state_time > 2500)
        goto look_around_left;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot= 20; //90
      head.control_mode = search_for_ball;      
    }
  }

  state turn_to_right {
    decision {
      else if(state_time > 2500)
        goto look_around_right;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot= -20; //-90
      head.control_mode = search_for_ball;      
    }
  }

  state look_around_left {
    decision {
      else if(state_time > 5000){
          goto turn_to_left;
      }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  state look_around_right {
    decision {
      else if(state_time > 5000){
          goto turn_to_right;
      }
      else
        stay;
    }
    action {
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }

  target state turn_to_ball 
  {
    decision {
      else if(ball.time_since_last_seen > 1000)
        goto look_around;
      else
        stay;
    }
    action {
      motion.type=walk;
      motion.walk_speed.x=0;
      motion.walk_speed.y=0;
      motion.walk_speed.rot=atan2(y=ball.preview.y, x=ball.preview.x);
      head.control_mode = search_for_ball;
    }
  }
}