/* If walking in a slight curve fails, read comment in the look for ball state */

option penalty_kicker
{
  initial state look_for_ball
  {
    decision
    {
      if(state_time > 3000)
      {
        if(ball.know_where_itis)
          goto approach_ball_right;
        else
          goto move_closer;
      }
      else
        stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }

  state move_closer 
  {
    decision
    {
      if(state_time > 2500)
      {
        if(ball.know_where_itis)
          goto approach_ball_right;
        else
          goto look_for_ball;
      }
      else
        stay;
    }
    action
    {
      motion.type = walk;
      motion.walk.style = stable;
      motion.walk_speed.x = 50;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;
      
      head.control_mode = look_at_ball_no_search;
    }
  }

  state approach_ball_right
  {
    decision
    {
      if (action_done)
        goto look_for_ball_tmp;//do_kick;
      else
        stay;
    }
    action
    {
      go_to_ball_with_foot(right=true, distance=160);
      head.control_mode = look_at_ball_no_search; // don't look around
      
    }
  }
  
  state look_for_ball_tmp
  {
    decision
    {
      if(state_time > 100)
      {
        goto do_kick;
      }
      else
        stay;
    }
    action
    { 
      motion.type = stand;
      head.control_mode = look_at_ball_no_search;
    }
  }

  state do_kick
  {
    decision
    {
      if(action_done)
        goto finished;
      else
        stay;
    }
    action
    {
      path_short_kick(foot = right);
      head.control_mode = look_at_ball_no_search;
      
    }
  }

  target state finished
  {
    decision
    {
      stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = look_straight_ahead;
    }
  }

}