/**
 * We have a free kick!
 * Walk close to the ball for an optimal (free) kick.
 * Shortly before the 'free kick time' expires, execute kick. There shouldn't be an opponent
 * near the ball and we can/should execute an 'optimal long' kick.
 */
option positioning_for_own_freekick
{
  initial state search_for_ball
  {
    decision {
      if( game.msecsRemainingSecondary <= 20000 ) // 20sec until free kick is over
        goto decide_role;
      else if( state_time > 5000 || ball.know_where_itis)
        goto look_at_ball;
      else
        stay;
    }
    action {
      situation_abortable = true;
      search_ball();
    }
  }

  state look_at_ball {
    decision {
      if( game.msecsRemainingSecondary <= 20000 ) // 20sec until free kick is over
        goto decide_role;
      else if (!ball.know_where_itis)
        goto search_for_ball;
      else
        stay;
    }
    action {
      situation_abortable = true;
      head.control_mode = look_at_ball_percept;
      motion.type = stand;
    }
  }

  state decide_role {
    decision {
      if(state_time > 2000) { // after 2s should be a common decision reached
        if(team.members_alive_count <= 1 || team.calc_if_is_striker)
          goto goto_freekick_position;
        //else if(player)
        // NOTE: special handling for the goalie???
        else
          goto idle;
      } else
        stay;
    }
    action {
      head.control_mode = look_at_ball_percept;
      motion.type = stand;
    }
  }

  state goto_freekick_position {
    decision {
      if( game.msecsRemainingSecondary <= 5000 ) { // 5sec until free kick is over
        if(vector.abs(x=(ball.preview.x - 500 - ball.radius), y=ball.preview.y) < 100)
          goto free_kick;
        // NOTE: should we check, if we're still able to reach the ball in the remaining time?!
        else
          goto arrived;
      } else
        stay;
    }
    action {
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 500); 
    }
  }

  state free_kick {
    // TODO: should we stop anytime?!
    action {
      situation_abortable = true;
      path_kick_long_with_approach();
    }
  }

  state idle {
    // the kicker should approach the ball or already kicked the ball
    // the 'normal' game will continue soon, we can already play
    decision {
      if( game.msecsRemainingSecondary <= 4000 ) // 4sec until free kick is over
        goto arrived;
      else
        stay;
    }
    action {
      head.control_mode = look_at_ball_percept;
      motion.type = stand;
    }
  }

  target state arrived {
    action {
      head.control_mode = look_at_ball_percept;
      motion.type = stand;
    }
  }
}