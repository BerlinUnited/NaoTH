/** Go to the ball with respect to the given foot 
  Note: No rotation with less then 25 cm distance! 
  Parameter @distance for the distance foot-ball (use 120 as default)**/

option path_go_to_ball_with_foot_dynamic
{
  bool @right;
  float @distance [mm];
  float @offset.y [mm];


  initial state decide_foot
  {
    decision
    {
      if (@right)
        goto go_to_ball_right_foot;
      else
        goto go_to_ball_left_foot;
    }
    action
    {
    }
  }
  
  state go_to_ball_right_foot
  {
    decision
    {
      if ( (ball.preview.right_foot.x - @distance - ball.radius) < 20
        && abs(value = ball.preview.right_foot.y + @offset.y) < 10)
        //&& atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) < 15)
        goto go_to_ball_right_foot_done;
      else
        stay;
    }
    action 
    {
      path.routine = approach_ball_right;
      path.distance = @distance;
      path.yOffset = @offset.y;

      head.control_mode = look_at_ball_percept;
    }
  }

  state go_to_ball_left_foot
  {
    decision
    {
      if ( (ball.preview.left_foot.x - @distance - ball.radius) < 20
        && abs(value = ball.preview.left_foot.y - @offset.y) < 10)
        //&& atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) < 15)
        goto go_to_ball_left_foot_done;
      else
        stay;
    }
    action 
    {
      path.routine = approach_ball_left;
      path.distance = @distance;
      path.yOffset = @offset.y;
      
      head.control_mode = look_at_ball_percept;
    }
  }

  target state go_to_ball_right_foot_done
  {
    decision
    {
      if (abs(value = (ball.preview.right_foot.x - @distance - ball.radius)) > 30
        || abs(value = ball.preview.right_foot.y + @offset.y) > 30)
        //|| atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) > 35)
        goto go_to_ball_right_foot;
      else
        stay;
    }
    action 
    {
      path.routine = approach_ball_right;
      path.distance = @distance;
      path.yOffset = @offset.y;
    }
  }

  target state go_to_ball_left_foot_done
  {
    decision
    {
      if (abs(value = (ball.preview.left_foot.x - @distance - ball.radius)) > 30
        || abs(value = ball.preview.left_foot.y - @offset.y) > 30)
        //|| atan2(y = ball.preview.right_foot.y, x = ball.preview.right_foot.x) > 35)
        goto go_to_ball_left_foot;
      else
        stay;
    }
    action 
    {
      path.routine = approach_ball_left;
      path.distance = @distance;
      path.yOffset = @offset.y;
    }
  }
}
