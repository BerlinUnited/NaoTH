option pathplanner2018_dash_test
{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if(situation_abortable && ball.time_since_last_seen > 1500)
      goto search;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 300)
      goto goto_ball;
  }

  initial state idle {
    decision {
      else
        goto search;
    }
    action {
      situation_abortable = true;
    }
  }

  state search {
    decision {
      else if (action_done)
        goto goto_ball;
      else
        stay;
    }
    action{
      situation_abortable = true;
      search_ball();
    }
  }

  state goto_ball {
    decision{

      else if(ball.preview.x < 300 && abs(value = ball.preview.y) < 50)
      {
        goto dash;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      /* this distance is smaller than the condition for ball.preview.x above
         possible unwanted behavior: the ball is closer than 300 but ball.preview.y is
         bigger than 50. The robot won't rotate in go_to_ball_with_USOA and will continue
         to walk to the position of the ball. Two possible outcomes:
         1. the robot runs into the ball (if ball distance is not considered)
         2. the robot will walk backwards in the successive approaching because it was too close*/
      go_to_ball_with_USOA(distance = 210); 
    }
  }

  state dash {
    decision{
      else if(action_done)
        goto done;
      else
        stay;
    }
    action{
      situation_abortable = true;
      path2018_dash();
    }
  }


  target state done
  {
    decision {
    else if(state_time > 500)
      goto idle;
      else
        stay;
    }
    action {
      //NOTE: this is synchronized with sidekick()->finished
      motion.type = stand;

      //look at percept
      head.control_mode = look_at_ball_percept;
    }
  }
}