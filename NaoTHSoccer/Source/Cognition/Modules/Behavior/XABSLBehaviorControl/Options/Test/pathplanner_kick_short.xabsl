option pathplanner_kick_short
{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if(situation_abortable && ball.time_since_last_seen > 1500)
      goto search;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 400)
      goto goto_ball;
  }

  initial state idle {
    decision {
      else
        goto search;
    }
    action {
      situation_abortable = true;
    }
  }

  state search {
    decision {
      else if (ball.know_where_itis && ball.preview.x > 300)
        goto goto_ball;
      else if(action_done)
        goto approach_short_kick_left;
      else
        stay;
    }
    action{
      situation_abortable = true;
      search_ball();
    }
  }

  state goto_ball {
    decision{

      else if(action_done && state_time > 1000)
      {
        goto approach_short_kick_left;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 300);
      
      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
  state approach_short_kick_left {
    decision {
      else if(action_done)
        goto short_kick_left;
      else
        stay;
    }
    action {
      path_go_to_ball_with_foot_dynamic(right = false, distance = 125, offset.y=0);
      
      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
  state short_kick_left {
    decision{
      else if(action_done)
        goto done;
      else
        stay;
    }
    action {
      situation_abortable = false;
      path_short_kick(foot = left);
      
      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
  target state done
  {
    decision {
    else if(state_time > 2000)
      goto idle;
      else
        stay;
    }
    action {
      //NOTE: this is synchronized with sidekick()->finished
      motion.type = stand;
    
      motion.walk.stop_with_stand = true;

      motion.walk.coordinate = hip;
      motion.walk.style = normal;

      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
}