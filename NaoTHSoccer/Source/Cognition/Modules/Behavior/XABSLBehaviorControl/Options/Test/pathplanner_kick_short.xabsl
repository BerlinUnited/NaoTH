option pathplanner_kick_short
{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 400)
      goto goto_ball;
  }

  initial state idle {
    decision {
      else
        goto goto_ball;
    }
    action {
      situation_abortable = true;
    }
  }

  state goto_ball {
    decision{

      else if(action_done && state_time > 1000)
      {
        if(ball.preview.y > 0)
          goto approach_short_kick_left;
        else
          goto approach_short_kick_right;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 250);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state approach_short_kick_left {
    decision {
      else if(action_done)
        goto short_kick_left;
      else
        stay;
    }
    action {
      path_go_to_ball_with_foot_dynamic(right = false, distance = 125, offset.y=0);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state short_kick_left {
    decision{
      else if(action_done)
        goto done;
      else
        stay;
    }
    action {
      situation_abortable = false;
      path_short_kick(foot = left);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state approach_short_kick_right {
    decision {
      else if(action_done)
        goto short_kick_right;
      else
        stay;
    }
    action {
      path_go_to_ball_with_foot_dynamic(right = true, distance = 125, offset.y=0);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state short_kick_right {
    decision{
      else if(action_done)
        goto done;
      else
        stay;
    }
    action {
      situation_abortable = false;
      path_short_kick(foot = right);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }

  target state done
  {
    decision {
    else if(state_time > 500)
      goto idle;
      else
        stay;
    }
    action {
      //NOTE: this is synchronized with sidekick()->finished
      motion.type = stand;

      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
}