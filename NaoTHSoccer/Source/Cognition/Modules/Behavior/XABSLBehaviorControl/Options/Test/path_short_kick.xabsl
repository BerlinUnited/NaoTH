option path_short_kick
{

  enum direction @foot;
  
  initial state wait
  {
    decision
    {
      if(executed_motion.type != walk)
        stay;
      else if(executed_motion.step_control.right_movable && @foot == right)
        goto kick_with_right_foot;
      else if(executed_motion.step_control.left_movable && @foot == left)
        goto kick_with_left_foot;
      else
        stay;
    }
    action
    {
      head.control_mode = search_for_ball;
      motion.walk.style = normal;

      motion.type = walk;
      motion.walk_speed.x = 0;
      motion.walk_speed.y = 0;
      motion.walk_speed.rot = 0;
    }
  }

  state kick_with_right_foot
  {
    decision
    {
      if(state_time > 300 && executed_motion.step_control.left_movable)
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path.routine = short_kick_right;
    }
  }

   state kick_with_left_foot
  {
    decision
    {
      if(state_time > 300 && executed_motion.step_control.right_movable)
        goto short_freeze;
      else
        stay;
    }
    action
    {
      path.routine = short_kick_left;
    }
  }

  state short_freeze {
    decision {
      if(state_time > 2000)
        goto finished;
      else 
        stay; 
    }
    action {
      path.routine = none;
    }
  
  }

  target state finished
  {
    decision
    {
      stay;
    }
    action
    {
      path.routine = none;
    }
  }
}