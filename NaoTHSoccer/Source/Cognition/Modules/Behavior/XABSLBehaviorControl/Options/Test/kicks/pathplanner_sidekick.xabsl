option pathplanner_sidekick{
  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if(situation_abortable && ball.time_since_last_seen > 1500)
      goto search;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 300)
      goto goto_ball;
  }

  initial state idle {
    decision {
      else
        goto search;
    }
    action {
      situation_abortable = true;
    }
  }

  state search {
    decision {
      else if(action_done)
        goto goto_ball;
      else
        stay;
    }
    action{
      situation_abortable = true;
      search_ball(x=robot_pose.x, y=robot_pose.y);
    }
  }

  state goto_ball {
    decision{

      else if(action_done && state_time > 1000)
      {
        goto sidekick_left;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 190);
      
      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
  
  state sidekick_left {
    decision {
      else if(action_done)
        goto goto_ball_2;
      else
        stay;
    }
    action {
      situation_abortable = true;
      path_sidekick_left();
    }
  }

  state goto_ball_2 {
    decision{

      else if(action_done && state_time > 1000)
      {
        goto sidekick_right;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 190);
      
      //loook at percept
      head.control_mode = look_at_ball_percept;
    }
  }
  
  state sidekick_right {
    decision {
      else if(action_done)
        goto goto_ball;
      else
        stay;
    }
    action {
      situation_abortable = true;
      path_sidekick_right();
    }
  }
}