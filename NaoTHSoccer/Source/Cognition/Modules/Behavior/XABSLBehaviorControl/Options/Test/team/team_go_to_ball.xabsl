option team_go_to_ball
{

  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
  }

  initial state idle {
    decision {
      else if(game.current_mode == playing && strategy.role_dynamic == striker)
        goto goto_ball_striker;
      else
        stay;
    }
    action {}
  }

  state goto_ball_striker
  {
    decision
    {
      else if (strategy.role_dynamic != striker)
        goto idle;
      else if(action_done)
        goto done_striker;
      else
        stay;
    }
    action 
    {
      team.is_playing_as_striker = true;
      go_to_ball(distance = 210);
    }
  }

  state done_striker
  {
    decision
    {
      else if(ball.time_seen > 100 && ball.distance > 500)
        goto idle;
      else
        stay;
    }
    action 
    {
      team.is_playing_as_striker = true;
      head.control_mode = search_for_ball;
      motion.type = stand;
    }
  }
}