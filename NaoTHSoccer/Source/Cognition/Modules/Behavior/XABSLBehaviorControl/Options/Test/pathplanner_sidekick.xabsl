option pathplanner_sidekick {

  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 300)
      goto goto_ball;
  }

  initial state idle {
    decision {
      else
        goto goto_ball;
    }
    action {
      situation_abortable = true;
    }
  }

  state goto_ball {
    decision{

      else if(action_done && state_time > 1000)
      {
        goto approach_sidekick_to_left;
      }
      else
        stay;
    }
    action{
      situation_abortable = true;
      path_go_to_ball_with_USOA(distance = 190);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state approach_sidekick_to_left {
    decision {
      else if(action_done)
        goto sidekick_to_left;
      else
        stay;
    }
    action {
      //go_to_ball_with_foot(right = true, distance = 130, offset.y=-10);
      path_go_to_ball_with_foot_dynamic(right = true, distance = 125, offset.y=-30);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  state sidekick_to_left {
    decision{
      else if(action_done)
        goto done;
      else
        stay;
    }
    action {
      situation_abortable = false;
      path_sidekick(to = left);
      
      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
  target state done
  {
    decision {
    else if(state_time > 100)
      goto idle;
      else
        stay;
    }
    action {
      //NOTE: this is synchronized with sidekick()->finished
      motion.type = stand;

      //loook at percept
      head.control_mode = search_for_ball;
    }
  }
}