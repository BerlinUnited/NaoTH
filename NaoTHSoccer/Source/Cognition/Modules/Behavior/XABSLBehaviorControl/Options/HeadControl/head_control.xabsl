/** The base option for the head control behavior */
option head_control
{
  common decision
  {
    if (head.control_mode == none)
      goto none;
    else if (head.control_mode == stay_as_forced)
      goto stay_as_forced;
    else if (head.control_mode == look_straight_ahead)
      goto look_straight_ahead;
    else if (head.control_mode == look_at_ball)
      goto look_at_ball;
    else if (head.control_mode == search_for_ball)
      goto search_for_ball;
    else if (head.control_mode == search_for_ball_goalie)
      goto search_for_ball_goalie;
    else if (head.control_mode == look_around)
      goto look_around;
    else if(head.control_mode == look_at_ball_percept)
      goto look_at_ball_percept;
    else if(head.control_mode == search_for_ball_and_look_around)
      goto search_for_ball_and_look_around;
    else if(head.control_mode == look_at_team_ball)
      goto look_at_team_ball;
   else if(head.control_mode == look_at_preview_angle)
      goto look_at_preview_angle;

  }

  initial state none
  {
    action
    {
    // do nothing
    }
  }
  
  state stay_as_forced
  {
    action
    {
      head.motion_type = hold;
      head.camera.id = Bottom;
    }
  }
    
  state look_straight_ahead
  {
    action
    {
      head.camera.id = Top;
      head.motion_type = look_straight_ahead;
    }
  }
  
  state look_at_ball
  {
    action
    {
      head.camera.id = Bottom;
      search_for_ball3(force_ball_model_check = false, look_around=false, look_at_ball_when_distance=1000);
    }
  }

  state search_for_ball
  {
    action
    {
      head.camera.id = Bottom;
      search_for_ball3(force_ball_model_check = false, look_around=true, look_at_ball_when_distance=1000);
    }
  }

  state search_for_ball_goalie
  {
    action
    {
      head.camera.id = Bottom;
      search_for_ball3(force_ball_model_check = false, look_around=true, look_at_ball_when_distance=4000, use_teamball=true);
    }
  }


  state search_for_ball_and_look_around
  {
    action
    {
      head.camera.id = Bottom;
      search_for_ball3(force_ball_model_check=false, look_around=true, look_at_ball_when_distance=1000);
    }
  }

  state look_around
  {
    action
    {
      head.camera.id = Top;
      look_around();
    }
  }


  state look_at_ball_percept 
  {
	  action 
	  {
      head.camera.id = Bottom;
      look_at_ball_percept();
	  }
	}

  state look_at_team_ball
  {
    action 
    {
      head.camera.id = Top;
      look_at_team_ball();
    }
  }
  
 state look_at_preview_angle{
   action{
     look_at_preview_angle();
   }
 }
 
}
  