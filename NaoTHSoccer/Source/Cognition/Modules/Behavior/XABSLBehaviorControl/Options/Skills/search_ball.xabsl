option search_ball
{  

  // stop first
  initial state walk_back
  {
    decision {
      if(ball.know_where_itis)
        goto finish;
      else //if(state_time > 2000)
      {
        //goto start;
        if(ball.preview.y > 0)
          goto search_ball_rot_left;
        else
          goto search_ball_rot_right;
      }
    }
    action {
      motion.type = walk;
      motion.walk_speed.x = -50; // mm
      motion.walk_speed.y = 0; // mm
      motion.walk_speed.rot = 0; //deg
      motion.walk.coordinate = hip;
      head.control_mode = search_for_ball;
    }
  }


  state start
  {
    decision {
      if(executed_motion.type == stand)
      {
	      if(ball.preview.y > 0)
	        goto search_ball_rot_left;
	      else
	        goto search_ball_rot_right;
      }
      else
        stay;
    }
    action {
      path.routine = none;
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
  
  state search_ball_rot_left
  {
    decision {
      if(action_done)
        goto search_ball_simple_right;
      else
        stay;
    }
    action {
      search_ball_back_rot(search_direction = left);
    }
  }

 state search_ball_rot_right
  {
    decision {
      if(action_done)
        goto search_ball_simple_left;
      else
        stay;
    }
    action {
      search_ball_back_rot(search_direction = right);
    }
  }

  state search_ball_simple_left
  {
    decision {
      if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_simple(search_direction = left);
      motion.walk.style = stable;
    }
  }

  state search_ball_simple_right
  {
    decision {
      if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_simple(search_direction = right);
      motion.walk.style = stable;
    }
  }

  target state finish {
    action{
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
}