option search_ball
{
  common decision
  {
    if(ball.know_where_itis)
      goto finish;
  }

  initial state decide
  {
    decision {
      // if we last saw the ball in >2m distance, we just start looking left/right
      else if(ball.distance > 2000) // 2m
        goto start;
      // otherwise walk back for some mm
      else 
        goto walk_back;
    }
    action {
      path.routine = none;
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
  
  state walk_back
  {
    decision {
      // dont leave the field!
      else if(abs(value=robot_pose.planned.x) > field.xPosOpponentGroundline || 
              abs(value=robot_pose.planned.y) > field.yPosLeftSideline)
        goto start;
      // walk back
      else if(action_done)
        goto start;
      else
        stay;
    }
    action {
      // walk back for max. 50cm
      search_ball_walk_back(d=500);
    }
  }

  state start
  {
    decision {
      else if(executed_motion.type == stand)
      {
	      if(ball.preview.y > 0)
	        goto search_ball_rot_left;
	      else
	        goto search_ball_rot_right;
      }
      else
        stay;
    }
    action {
      path.routine = none;
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
  
  state search_ball_rot_left
  {
    decision {
      else if(action_done)
        goto search_ball_simple_right;
      else
        stay;
    }
    action {
      search_ball_back_rot(search_direction = left);
    }
  }

 state search_ball_rot_right
  {
    decision {
      else if(action_done)
        goto search_ball_simple_left;
      else
        stay;
    }
    action {
      search_ball_back_rot(search_direction = right);
    }
  }

  state search_ball_simple_left
  {
    decision {
      else if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_simple(search_direction = left);
      motion.walk.style = stable;
    }
  }

  state search_ball_simple_right
  {
    decision {
      else if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_simple(search_direction = right);
      motion.walk.style = stable;
    }
  }

  target state finish {
    action{
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
}