option search_ball
{
  common decision
  {
    if(ball.know_where_itis)
      goto finish;
  }

  initial state decide
  {
    decision {
      // if we last saw the ball in >2m distance, we just start looking left/right
      else if(ball.distance > 2000) // 2m
      {
        // search for ball the ball in the direction last seen
        if(ball.last_known.y > 0)
          goto search_ball_simple_left;
        else
          goto search_ball_simple_right;
      } else {
        // otherwise walk back for some mm and search for ball the ball in the direction last seen
        if(ball.last_known.y > 0)
          goto search_ball_rot_left;
        else
          goto search_ball_rot_right;
      }
    }
    action {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
/*
  // stand, before starting to walk back
  state start
  {
    decision {
      else if(executed_motion.type == stand)
      {
        if(ball.preview.y > 0)
          goto search_ball_rot_left;
        else
          goto search_ball_rot_right;
      }
      else
        stay;
    }
    action {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
*/
  state search_ball_rot_left
  {
    decision {
      else if(action_done)
        goto search_ball_simple_right;
      else
        stay;
    }
    action {
      search_ball_back_rot(distance = 250, max_rotation = 30, search_direction = left);
    }
  }

 state search_ball_rot_right
  {
    decision {
      else if(action_done)
        goto search_ball_simple_left;
      else
        stay;
    }
    action {
      search_ball_back_rot(distance = 250, max_rotation = 30, search_direction = right);
    }
  }

  state search_ball_simple_left
  {
    decision {
      else if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_turning(search_direction = left);
      motion.walk.style = stable;
    }
  }

  state search_ball_simple_right
  {
    decision {
      else if(action_done)
        goto finish;
      else
        stay;
    }
    action {
      search_ball_turning(search_direction = right);
      motion.walk.style = stable;
    }
  }

  target state finish {
    action{
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
}