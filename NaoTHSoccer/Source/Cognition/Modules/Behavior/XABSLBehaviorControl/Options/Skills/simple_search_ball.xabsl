/** just turning and looking for ball */
option simple_search_ball
{
  enum direction @search_direction;

  common decision  {
	  if(ball.was_seen || ball.time_since_last_seen < 100)
	    goto finish;
	}

  initial state start_turning
  {
    decision
    {
      else if ( @search_direction == right )
        goto turning_right;
      else
        goto turning_left;
    }
    action
    {
    }
  }

  state turning_left
  {
    decision
    {
      else 
        stay;
    }
    action
    {
      head.control_mode = none;

      head.motion_type = look_at_world_point;
      head.point_in_world.x = 200;
      head.point_in_world.y = 200;
      head.point_in_world.z = 0;
      
      motion.type = walk;
      motion.walk_speed.x = 0; // mm
      motion.walk_speed.y = 0; // mm
      motion.walk_speed.rot = 20; //deg
      motion.walk.coordinate = hip;
    }
  }

  state turning_right
  {
    decision
    {
      else
        stay;
    }
    action
    {    
      head.control_mode = none;

      head.motion_type = look_at_world_point;
      head.point_in_world.x = 200;
      head.point_in_world.y = -200;
      head.point_in_world.z = 0;

      motion.type = walk;
      motion.walk_speed.x = 0; // mm
      motion.walk_speed.y = 0; // mm
      motion.walk_speed.rot = -20; //deg
      motion.walk.coordinate = hip;
    }
  }

  target state finish
  {
    decision
    {
      else if (ball.time_since_last_seen > 500)
        goto start_turning;
      else
        stay;
    }
    action
    {
      motion.type = stand;
      head.control_mode = search_for_ball;
    }
  }
}
