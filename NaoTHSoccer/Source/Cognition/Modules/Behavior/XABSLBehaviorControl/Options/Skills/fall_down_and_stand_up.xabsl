option fall_down_and_stand_up
{
  initial state idle
  {
    decision
    {
      // HACK: no standup in init or bdr/naoqi behavior
      if(executed_motion.type == init
         || executed_motion.type == bdr_sit)
        stay;
      else if(abs(value=sensor.inertial.y)>30
          || abs(value=sensor.inertial.x)>30)
        goto falling;
      else
        stay;
    }      
    action
    {
      //HACK: body.ready_to_walk = true has to be set outside
      //body.ready_to_walk = true;
      // remember the arms position for the case the robot falls backwards
      // (used by stand up)
      arm.control_mode_before_fall = arm.control_mode;
    }
  }
  
  state falling
  {
    decision
    {
      if(abs(value = robot_pose.x) < 693
         && abs(value = robot_pose.y) < 346)
      {
	      if (fall_down_state != upright)
	        goto wait_before_stand_up;
	      else if (state_time > 1000)
	        goto stand_up;
	      else 
	        stay;
      } 
      else if(abs(value=sensor.inertial.y) < 5
              && abs(value=sensor.inertial.x) < 5)
          goto stand;
      else
          stay;
    }
    action
    {
      body.ready_to_walk = false;
      body.falling = true;
      motion.execute_immediately = true;
      motion.type = dead;
    }
  }

  state wait_before_stand_up
  {
    decision
    {
      if (state_time > 500)
        goto stand_up;
      else 
        stay;
    }
    action
    {
      body.ready_to_walk = false;
      motion.execute_immediately = true;
      motion.type = dead;
    }
  }

  state stand_up
  {
    decision
    {
      if(action_done)
        goto stand;
      else
        stay;
    }
    action 
    {
      body.ready_to_walk = false;
      motion.execute_immediately = false;
      stand_up();
    }
  }

  state stand
  {
    decision
    {
      if(executed_motion.type == stand)
        goto wait_for_completed_stand;
      else 
        stay;
    }      
    action
    {
      body.ready_to_walk = false;
      motion.type=stand;
      arm.control_mode = normal;
    }
  }

  state wait_for_completed_stand
  {
    decision
    {
      if(state_time > 1500)
        goto idle;
      else
        stay;
    }

    action
    {
      body.ready_to_walk = false;
      motion.type=stand;
      arm.control_mode = normal;
    }
  }
  
  
}