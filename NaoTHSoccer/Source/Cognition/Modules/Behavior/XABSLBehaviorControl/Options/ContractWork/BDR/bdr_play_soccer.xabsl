option bdr_play_soccer
{

  common decision
  {
    if(!body.ready_to_walk || game.current_mode != playing)
      goto idle;
  }

  initial state idle {
    decision {
      else
        goto goto_home_position;
    }
    action {
    }
  }

  state search {
    decision 
    {
      else if(ball.know_where_itis && ball.inOwnHalf)
        goto striker;
      else if(ball.know_where_itis || state_time > 10000)
        goto goto_home_position;
      else 
        stay;
    }

    action {
      bdr_search();
    }
  }

  state goto_home_position 
  {
    decision 
    {
      else if(ball.know_where_itis && ball.inOwnHalf)
        goto striker;
      else if(action_done && !ball.know_where_itis)
        goto search;
      else
          stay;
    }

    action {
      situation_abortable = true;
      bdr_goto_point(x = -600, y = 0, rot = 0);
      head.control_mode = look_around;
    }
  }

  state striker {
    decision {
      else if(situation_abortable && !ball.know_where_itis)
        goto search;
      else if(situation_abortable && ball.know_where_itis && !ball.inOwnHalf)
        goto goto_home_position;
      else
        stay;
    }

    action {
        situation_abortable = true;
        bdr_striker();
    }
  }

}