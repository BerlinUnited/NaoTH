option bdr_remote_autonomous{

    common decision
	  {
	    if(!body.ready_to_walk || game.current_mode != playing)
	      goto idle;
	    else if(situation_abortable && !ball.know_where_itis)
	      goto search;
	    else if(situation_abortable && vector.abs(x=ball.preview.x, y=ball.preview.y) > 300)
	      goto goto_ball;
	  }

    initial state idle {
	    decision {
	      else
	        goto search;
	    }
	    action {
	      situation_abortable = true;
	    }
	  }

    state search {
      decision {
        else if(action_done)
          goto goto_ball;
        else
          stay;
      }
      action{
        situation_abortable = true;
        search_ball();
      }
    }

    state goto_ball {
        decision {
            else if (action_done)
                goto kick;
            else
                stay;
        }

        action {
            situation_abortable = true;
            go_to_ball(distance = 140);
        }
    }

    state kick {
      decision {
            else if (action_done)
                goto wait;
            else
                stay;
        }
        action {
            situation_abortable = false;
            path_kick_short_with_approach();
        }
    }

    
    target state wait {
        decision {
            else if(state_time < 5000)
              stay;
            else
              goto search;
        }

        action {
            situation_abortable = false;
            head.control_mode = search_for_ball;
            motion.type = stand;
        }
    }
}