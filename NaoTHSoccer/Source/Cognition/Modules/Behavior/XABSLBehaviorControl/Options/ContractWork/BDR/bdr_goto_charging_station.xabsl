option bdr_goto_charging_station
{
  initial state wait {
      decision {
          if(state_time < 2000)
            stay;
          else
            goto approach;
      }

      action {
          head.control_mode = look_around;
          motion.type = stand;
      }
  }

  state approach {
      decision {
        if(action_done)
          goto wait_and_localize;
        else
          stay;
      }

      action {
          head.control_mode = look_around;
          bdr_goto_point(x = 0, y = 400, rot = -90);
          motion.walk.style = stable;
      }
  }

  state wait_and_localize{
      decision {
          if(state_time > 10000) {
              goto approach_very_slowly;
          }
          else
              stay;
      }
      
      action{
          motion.type = stand;
          head.control_mode = look_around;
      }
  }

  state approach_very_slowly{
      decision{
          if (action_done)
              goto sit;
          else
              stay;
      }

      action {
            head.control_mode = look_around;
            bdr_align_carefully(x = 0, y = 550, rot = -90);
      }
  }

  state sit {
      decision {
        if(executed_motion.type == bdr_sit)
          goto done;
        else
          stay;
      }

      action {
          motion.type = bdr_sit;
      }
  }

  target state done {
      decision {
        stay;
      }

      action {
          motion.type = bdr_sit;
      }
  }

}